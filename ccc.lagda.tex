\begin{code}[hide]
{-# OPTIONS --cubical #-}

module ccc where

open import contextual

open import Cubical.Categories.Category

private
  variable
    ℓ ℓ₁ ℓ₂ ℓ₃ ℓ₄ ℓ₅ ℓ₆ : Level

-- Here is a definition of a Cartesian Closed Contextual Category
\end{code}

\section{Theories For Lambda Calculus}

It is not the case that every single type theory has a notion of function types.
One, could, for example, consider a type theory which only has base and product
types; the metatheory of such a type system is non-trivial and one could
replicate the overall yoga used in this paper to \emph{Objectively} derive a
provably correct NbE algorithm for all initial languages that roughly look like
their syntax has pairing and projections.

Similarly, it is not true that every type theory that has a notion of functions
behaves like STLC. For example, untyped lambda calculus is certainly a language
of functions. One thinks of it as a \emph{unityped} theory in which, presented
as a contextual category, we would have $\mathsf{ty} = \top$. Though it appears
that such a $\mathsf{ty}$ is incapable of describing functions, the way in which
functions very meaningfully make sense in untyped lambda calculus is that there
is a natural contextual functor between from STLC to untyped lambda calculus
given by \emph{type erasure}.

As another example, we can, in Agda, construct a contextual category whose
$\mathsf{ty}$ is the type of h-sets in some universe. The set of terms between a
context $\Gamma$ and a type $A$ is given by forming an iterated function type,
i.e. such that $\mathsf{tm}~\varnothing~A = A$, and $\mathsf{tm}~(\Gamma
\hermitmatrix B)~A = B \to (\mathsf{tm}~\Gamma~A)$ (these will also be h-sets).
In order to specify how to interpret STLC into this theory, we specify, for each
base type $X$, an h-set $\lbkt{X}$. We then have $\lbkt{A \Rightarrow B} =
\lbkt{A} \to \lbkt{B}$. Finally, since the type system that we are programming
in is sufficiently elaborate, we can extend this to an interpretation of STLC
into Cubical Agda.
\iffalse
(Of course, if we interpret a base type $X$ as the set
$\mathbb{N}$, then the successor function $\mathbb{N} \to \mathbb{N}$ will not
be the interpretation of any closed STLC term of type $X \to X$, so we are
interpreting into a much stronger, dependently typed, theory.)
\fi

\subsection{CCC Categories}

Up to this point in the paper, we have been quite coy about what precisely we
mean by STLC, although we expect the reader to have vague ability to recognise
an STLC implementation when she sees one. We will proceed by giving the notion
of what it means for a contextual category to admit an interpretation of STLC,
and explain how this notion gives rise to a notion of what it means to be
\emph{an implementation of STLC}. Finally, near the end of the paper, we will
construct, according to this meaning, a suitable implementation, which one
should recognise as \emph{`being STLC'} in the \emph{ontical/pre-categorical}
sense of the term.

\begin{code}
record CCC (𝒞 : Contextual ℓ₁ ℓ₂) : Type (ℓ₁ ⊔ ℓ₂) where
  open Contextual 𝒞

  field
    _⇛_ : ty → ty → ty
    Λ : {Γ : ctx} {A B : ty} → tm (Γ ⊹ A) B → tm Γ (A ⇛ B)
    𝑎𝑝𝑝 : {Γ : ctx} {A B : ty} → tm Γ (A ⇛ B) → tm Γ A → tm Γ B

  -- Categorical app operator
  𝐴𝑝𝑝 : {Γ : ctx} {A B : ty} → tm Γ (A ⇛ B) → tm (Γ ⊹ A) B
  𝐴𝑝𝑝 t = 𝑎𝑝𝑝 (t ⟦ π ⟧) 𝑧

  field
    Λnat : {Γ Δ : ctx} {A B : ty} (t : tm (Δ ⊹ A) B) (σ : tms Γ Δ) →
      Λ t ⟦ σ ⟧ ≡  Λ ( t ⟦ W₂tms A σ ⟧)
    𝑎𝑝𝑝β : {Γ : ctx} {A B : ty} (t : tm (Γ ⊹ A) B) (s : tm Γ A) →
      𝑎𝑝𝑝 (Λ t) s ≡ t ⟦ 𝒾𝒹 Γ ⊕ s ⟧
    𝑎𝑝𝑝η : {Γ : ctx} {A B : ty} (t : tm Γ (A ⇛ B)) →
      t ≡ Λ (𝐴𝑝𝑝 t)
\end{code}

\begin{code}[hide]
  ⊕⊚ : {Γ Δ Σ : ctx} {A : ty} (σ : tms Δ Σ) (t : tm Δ A) (τ : tms Γ Δ) →
    (σ ⊕ t) ⊚ τ ≡ (σ ⊚ τ) ⊕ (t ⟦ τ ⟧)
  ⊕⊚ σ t τ =
    (σ ⊕ t) ⊚ τ
      ≡⟨ π𝑧η (σ ⊕ t ⊚ τ) ⁻¹ ⟩
    π ⊚ (σ ⊕ t ⊚ τ) ⊕ 𝑧 ⟦ σ ⊕ t ⊚ τ ⟧
      ≡⟨ (λ i → ⊚Assoc π (σ ⊕ t) τ (~ i) ⊕ ⟦⟧⟦⟧ 𝑧 (σ ⊕ t) τ (~ i)) ⟩
    π ⊚ (σ ⊕ t) ⊚ τ ⊕ 𝑧 ⟦ σ ⊕ t ⟧ ⟦ τ ⟧
      ≡⟨ (λ i → (πβ (σ ⊕ t) i ⊚ τ) ⊕ (𝑧β (σ ⊕ t) i ⟦ τ ⟧)) ⟩
    (σ ⊚ τ) ⊕ (t ⟦ τ ⟧)
      ∎

  private
    lem : {Γ Δ : ctx} {A : ty} (σ : tms Γ Δ) (t : tm Γ A) →
      ((σ ⊚ π) ⊕ 𝑧) ⊚ (𝒾𝒹 Γ ⊕ t) ≡ σ ⊕ t
    lem {Γ} σ t =
      ((σ ⊚ π) ⊕ 𝑧) ⊚ (𝒾𝒹 Γ ⊕ t)
        ≡⟨ ⊕⊚ (σ ⊚ π) 𝑧 (𝒾𝒹 Γ ⊕ t) ⟩
      σ ⊚ π ⊚ (𝒾𝒹 Γ ⊕ t) ⊕ 𝑧 ⟦ 𝒾𝒹 Γ ⊕ t ⟧
        ≡⟨ (λ i → ⊚Assoc σ π (𝒾𝒹 Γ ⊕ t) i ⊕ 𝑧β (𝒾𝒹 Γ ⊕ t) i) ⟩
      σ ⊚ (π ⊚ (𝒾𝒹 Γ ⊕ t)) ⊕ t
        ≡⟨ (λ i → σ ⊚ (πβ (𝒾𝒹 Γ ⊕ t) i) ⊕ t) ⟩
      σ ⊚ 𝒾𝒹 Γ ⊕ t
        ≡⟨ ap (_⊕ t) (𝒾𝒹R σ) ⟩
      σ ⊕ t
        ∎
\end{code}

This comprises the notion of what it means for a contextual category to be
\textbf{contextual cartesian closed} \emph{(CCC)}. A CCC category has a binary
$\Rrightarrow$ operation on types, along with notions of abstraction and
application. These satisfy the $\beta$ and $\eta$ laws, as well as a naturality
condition that explains how to substitute into an abstraction.

Thinking about the case of untyped lambda calculus, we see how this notion
captures what it means for the theory to \emph{`have functions'} without having
a dedicated function type. Here, we have that $\_\Rrightarrow\_ : \top \to \top
\to \top$ is constant, and one should consider to meaningless to ask whether
$\star : \top$ is of the form $\star_1 \Rrightarrow \star_2$. Nevertheless,
untyped lambda calculus has a suitable notion of abstraction and application
and thus gives rise to a CCC category.

The notion of a CCC category has special significance in category theory. To see
this, first note that the following property may be derived in any CCC
structure:
\begin{code}
  𝐴𝑝𝑝β : {Γ : ctx} {A B : ty} (t : tm (Γ ⊹ A) B) →
    𝐴𝑝𝑝 (Λ t) ≡ t
\end{code}
Combined with $\mathit{App}\eta$, this says that $\mathit{App}$ and $\Lambda$
are inverse bijections $\mathsf{tm}~(\Gamma \hermitmatrix A)~B \cong
{\mathsf{tm} ~\Gamma~(A \Rrightarrow B)}$. Keeping $A$ fixed, $Λ\mathsf{nat}$
asserts that this family of bijections is natural with respect to $\Gamma$.
Naturality in $B$ will follow from what we already have, but is rather delicate
to state, and is absent from the codebase. All together, this categorically says
that there is an adjunction $(\blank~\hermitmatrix~A) \dashv (A \Rrightarrow
\blank)$. (Note that the presence of $\mathsf{W}_\mathit{2} \mathsf{tms}$ in the
statement of $Λ\mathsf{nat}$ is reflective of the fact that the left functor
acts on substitutions by weakening.)

Note that all contextual categories have products. Specifically, the product
of a context and a type is formed using $\hermitmatrix$, and the product of
contexts is given by appending lists (although this notion is not useful).
The above thus states that the product has a right adjoint. Such an adjoint is
known as an \emph{exponential} (the name is derived from the set theoretic
notation of $B^A$ for $A \Rrightarrow B$), and this is the standard notion in
category theory of what it means to have function space objects.

$\mathit{App}$ and $\mathit{app}$ are respectively known as the
\emph{categorical} and \emph{programmatical} application operators, as the
former is closer to category theory and the latter to programming language
implementation. Above, we derived $\mathit{App}$ from $\mathit{app}$, although
we could have just as well worked in reverse by setting $\mathit{app}~t~s =
\mathit{App}~t~\lbkt{~\mathcal{id}~\Gamma \oplus s~}$. Simmilarly, the law
$\mathit{app}\beta$ could have instead been derived from the law
$\mathit{App}\beta$. Our construction is thus an equivalent reformulation of a
very well established notion of category theory: \emph{we say that a contextual
category admits an interpretation of STLC if it has exponentials}.

\begin{code}[hide]
  𝐴𝑝𝑝β {Γ} {A} {B} t =
    𝑎𝑝𝑝 (Λ t ⟦ π ⟧) 𝑧
      ≡⟨ (λ i → 𝑎𝑝𝑝 (Λnat t π i) 𝑧) ⟩
    𝑎𝑝𝑝 (Λ (t ⟦ (π ⊚ π) ⊕ 𝑧 ⟧)) 𝑧
      ≡⟨ 𝑎𝑝𝑝β (t ⟦ (π ⊚ π) ⊕ 𝑧 ⟧) 𝑧 ⟩
    t ⟦ (π ⊚ π) ⊕ 𝑧 ⟧ ⟦ 𝒾𝒹 (Γ ⊹ A) ⊕ 𝑧 ⟧
      ≡⟨ ⟦⟧⟦⟧ t ((π ⊚ π) ⊕ 𝑧) (𝒾𝒹 (Γ ⊹ A) ⊕ 𝑧) ⟩
    t ⟦ ((π ⊚ π) ⊕ 𝑧) ⊚ (𝒾𝒹 (Γ ⊹ A) ⊕ 𝑧) ⟧
      ≡⟨ ap (t ⟦_⟧) (lem π 𝑧) ⟩
    t ⟦ π ⊕ 𝑧 ⟧
      ≡⟨ ap (t ⟦_⟧) 𝒾𝒹η ⟩
    t ⟦ 𝒾𝒹 (Γ ⊹ A) ⟧
      ≡⟨ 𝒾𝒹⟦⟧ t ⟩
    t
      ∎

  𝐴𝑝𝑝⟦⟧ : {Γ Δ : ctx} {A B : ty} (t : tm Δ (A ⇛ B)) (σ : tms Γ Δ) →
    𝐴𝑝𝑝 (t ⟦ σ ⟧) ≡ (𝐴𝑝𝑝 t ⟦ σ ⊚ π ⊕ 𝑧 ⟧)
  𝐴𝑝𝑝⟦⟧ t σ =
    𝐴𝑝𝑝 (t ⟦ σ ⟧)
      ≡⟨ (λ i → 𝐴𝑝𝑝 (𝑎𝑝𝑝η t i ⟦ σ ⟧)) ⟩
    𝐴𝑝𝑝 (Λ (𝐴𝑝𝑝 t) ⟦ σ ⟧)
      ≡⟨ ap 𝐴𝑝𝑝 (Λnat (𝐴𝑝𝑝 t) σ) ⟩
    𝐴𝑝𝑝 (Λ (𝐴𝑝𝑝 t ⟦ σ ⊚ π ⊕ 𝑧 ⟧))
      ≡⟨ 𝐴𝑝𝑝β (𝐴𝑝𝑝 t ⟦ σ ⊚ π ⊕ 𝑧 ⟧) ⟩
    𝐴𝑝𝑝 t ⟦ σ ⊚ π ⊕ 𝑧 ⟧
      ∎

  𝑎𝑝𝑝𝐴𝑝𝑝 : {Γ : ctx} {A B : ty} (t : tm Γ (A ⇛ B)) (s : tm Γ A) →
    𝑎𝑝𝑝 t s ≡ 𝐴𝑝𝑝 t ⟦ 𝒾𝒹 Γ ⊕ s ⟧
  𝑎𝑝𝑝𝐴𝑝𝑝 {Γ} t s =
    𝑎𝑝𝑝 t s
      ≡⟨ (λ i → 𝑎𝑝𝑝 (𝑎𝑝𝑝η t i) s) ⟩
    𝑎𝑝𝑝 (Λ (𝐴𝑝𝑝 t)) s
      ≡⟨ 𝑎𝑝𝑝β (𝐴𝑝𝑝 t) s ⟩
    𝐴𝑝𝑝 t ⟦ 𝒾𝒹 Γ ⊕ s ⟧
      ∎
\end{code}

On an entirely different note, the codebase includes five lemmas aimed at
deriving the following:
\begin{code}
  -- We finally get to the substitution law for applications;
  -- this follows from the axioms, with great difficulty.
  𝑎𝑝𝑝⟦⟧ : {Γ Δ : ctx} {A B : ty} (t : tm Δ (A ⇛ B)) (s : tm Δ A) (σ : tms Γ Δ) →
    𝑎𝑝𝑝 t s ⟦ σ ⟧ ≡ 𝑎𝑝𝑝 (t ⟦ σ ⟧) (s ⟦ σ ⟧)
\end{code}
The usual presentation of STLC has three syntactic categories of terms:
\emph{variables}, \emph{abstraction}, and \emph{application}. In the previous
section, we saw the law $\mathsf{var}\lbkt{}$ describing the effect of
substituting into a variable. This is now joined by the laws
$\Lambda\mathsf{nat}$ and $\mathit{app}\lbkt{}$, which describe how to
substitute into abstractions and applications.
\begin{code}[hide]
  𝑎𝑝𝑝⟦⟧ {Γ} {Δ} t s σ =
    𝑎𝑝𝑝 t s ⟦ σ ⟧
      ≡⟨ ap (_⟦ σ ⟧) (𝑎𝑝𝑝𝐴𝑝𝑝 t s) ⟩
    𝐴𝑝𝑝 t ⟦ 𝒾𝒹 Δ ⊕ s ⟧ ⟦ σ ⟧
      ≡⟨ ⟦⟧⟦⟧ (𝐴𝑝𝑝 t) (𝒾𝒹 Δ  ⊕ s) σ ⟩
    𝐴𝑝𝑝 t ⟦ 𝒾𝒹 Δ ⊕ s ⊚ σ ⟧
      ≡⟨ ap (𝐴𝑝𝑝 t ⟦_⟧) (⊕⊚ (𝒾𝒹 Δ) s σ) ⟩
    𝐴𝑝𝑝 t ⟦ (𝒾𝒹 Δ) ⊚ σ ⊕ s ⟦ σ ⟧ ⟧
      ≡⟨ (λ i → 𝐴𝑝𝑝 t ⟦ 𝒾𝒹L σ i ⊕ s ⟦ σ ⟧ ⟧) ⟩
    𝐴𝑝𝑝 t ⟦ σ ⊕ s ⟦ σ ⟧ ⟧
      ≡⟨ ap (𝐴𝑝𝑝 t ⟦_⟧) (lem σ (s ⟦ σ ⟧) ⁻¹) ⟩
    𝐴𝑝𝑝 t ⟦ (σ ⊚ π ⊕ 𝑧) ⊚ (𝒾𝒹 Γ ⊕ s ⟦ σ ⟧) ⟧
      ≡⟨ ⟦⟧⟦⟧ (𝐴𝑝𝑝 t) (σ ⊚ π ⊕ 𝑧) (𝒾𝒹 Γ ⊕ s ⟦ σ ⟧) ⁻¹ ⟩
    𝐴𝑝𝑝 t ⟦ σ ⊚ π ⊕ 𝑧 ⟧ ⟦ 𝒾𝒹 Γ ⊕ s ⟦ σ ⟧ ⟧
      ≡⟨ ap _⟦ 𝒾𝒹 Γ ⊕ s ⟦ σ ⟧ ⟧ (𝐴𝑝𝑝⟦⟧ t σ ⁻¹) ⟩
    𝐴𝑝𝑝 (t ⟦ σ ⟧) ⟦ 𝒾𝒹 Γ ⊕ s ⟦ σ ⟧ ⟧
      ≡⟨ 𝑎𝑝𝑝𝐴𝑝𝑝 (t ⟦ σ ⟧) (s ⟦ σ ⟧) ⁻¹ ⟩
    𝑎𝑝𝑝 (t ⟦ σ ⟧) (s ⟦ σ ⟧)
      ∎

  -- A transport lemma

  transp𝐴𝑝𝑝 : {Γ Δ : ctx} {A B : ty} (a : Γ ≡ Δ) (t : tm Γ (A ⇛ B)) →
    transport (λ i → tm (a i ⊹ A) B) (𝐴𝑝𝑝 t) ≡ 𝐴𝑝𝑝 (transport (λ i → tm (a i) (A ⇛ B)) t)
  transp𝐴𝑝𝑝 {A = A} {B} a t =
    J (λ Δ a → transport (λ i → tm (a i ⊹ A) B) (𝐴𝑝𝑝 t)
      ≡ 𝐴𝑝𝑝 (transport (λ i → tm (a i) (A ⇛ B)) t))
      (transportRefl (𝐴𝑝𝑝 t) ∙ ap 𝐴𝑝𝑝 (transportRefl t ⁻¹)) a
\end{code}

\subsection{CCC Preserving Contextual Functors}

The idea behind the \emph{type erasure} functor between STLC and untyped lambda
calculus is that abstractions should go to abstractions and applications should
go to applications (that variables go to variables follows from the fact that
contextual functors preserve the identity). This requirement in fact, as will be
evident from our subsequent exposition, results in a propositionally unique
morphism. This situation suggests that we should articulate what it means for a
contextual functor between CCC categories to preserve CCC structures.

The definition appears below. It is rather plain to assert what it means for $F$
to preserve $\Rrightarrow$, but for all other constructions, we run into the
problem that if $t : \mathsf{tm}~\Gamma~(A \Rrightarrow B)$, then
$\mathsf{CF\mhyphen tm}~t$ has codomain type $\mathsf{CF\mhyphen ty}~(A
\Rrightarrow B)$, and this is only propositionally, but not definitionally, an
arrow type. The statements for what it means to preserve $\mathit{App}$,
$\mathit{app}$, and $\Lambda$ thus all involve transports.
\begin{code}
record CCCPreserving {𝒞 : Contextual ℓ₁ ℓ₂} {𝒟 : Contextual ℓ₃ ℓ₄}
       ⦃ 𝒞CCC : CCC 𝒞 ⦄ ⦃ 𝒟CCC : CCC 𝒟 ⦄ (F : ContextualFunctor 𝒞 𝒟)
       : Type (ℓ₁ ⊔ ℓ₂ ⊔ ℓ₃ ⊔ ℓ₄) where

  private
    module C = Contextual 𝒞
    module D = Contextual 𝒟
    module Cc = CCC 𝒞CCC
    module Dc = CCC 𝒟CCC

  open ContextualFunctor F

  field
    pres-⇛ : (A B : C.ty) → CF-ty (A Cc.⇛ B) ≡ CF-ty A Dc.⇛ CF-ty B
    pres-𝐴𝑝𝑝 : {Γ : C.ctx} {A B : C.ty} (t : C.tm Γ (A Cc.⇛ B)) →
      CF-tm (Cc.𝐴𝑝𝑝 t)
        ≡ Dc.𝐴𝑝𝑝 (transport (λ i → D.tm (CF-ctx Γ) (pres-⇛ A B i)) (CF-tm t))
\end{code}

Note that we made the surprising choice of only requiring the user to show the
preservation of $\mathit{App}$. This is sufficient to derive the preservation
of $\mathit{app}$ and $\Lambda$ since $\mathit{App}$ and $\mathit{app}$ are
mutually derivable, and since $\mathit{App}$ and $\Lambda$ are inverse.
We have found that, of the three, $\mathit{App}$ is probably the most amenable
requierment to set.
\begin{AgdaMultiCode}
\begin{code}
  pres-Λ : {Γ : C.ctx} {A B : C.ty} (t : C.tm (Γ ⊹ A) B) →
    PathP (λ i → D.tm (CF-ctx Γ) (pres-⇛ A B i)) (CF-tm (Cc.Λ t)) (Dc.Λ (CF-tm t))
\end{code}
\begin{code}[hide]
  pres-Λ {Γ} {A} {B} t =
    toPathP
      (transport (λ i → D.tm (CF-ctx Γ) (pres-⇛ A B i)) (CF-tm (Cc.Λ t))
        ≡⟨ Dc.𝑎𝑝𝑝η (transport (λ i → D.tm (CF-ctx Γ) (pres-⇛ A B i)) (CF-tm (Cc.Λ t))) ⟩
      Dc.Λ (Dc.𝐴𝑝𝑝 (transport (λ i → D.tm (CF-ctx Γ) (pres-⇛ A B i)) (CF-tm (Cc.Λ t))))
        ≡⟨ ap Dc.Λ (pres-𝐴𝑝𝑝 (Cc.Λ t) ⁻¹) ⟩
      Dc.Λ (CF-tm (Cc.𝐴𝑝𝑝 (Cc.Λ t)))
        ≡⟨ (λ i → Dc.Λ (CF-tm (Cc.𝐴𝑝𝑝β t i))) ⟩
      Dc.Λ (CF-tm t)
        ∎)
\end{code}
\begin{code}

  pres-𝑎𝑝𝑝 : {Γ : C.ctx} {A B : C.ty} (t : C.tm Γ (A Cc.⇛ B)) (s : C.tm Γ A) →
    CF-tm (Cc.𝑎𝑝𝑝 t s)
      ≡ Dc.𝑎𝑝𝑝 (transport (λ i → D.tm (CF-ctx Γ) (pres-⇛ A B i)) (CF-tm t)) (CF-tm s)
\end{code}
\end{AgdaMultiCode}

Finally, we show that the identity CF on a CCC category is CCC preserving, and
that the composition of CCC preserving CFs is CCC preserving. For the latter,
the required type of the $\mathsf{pres\mhyphen}\mathit{App}$ field involves two
layers of transport (one coming from the definition of composition and the other
from above) that have to be commuted in a non-trivial way, squarely landing one
back in \emph{transport hell (ninth circle)}.

\begin{code}[hide]
  pres-𝑎𝑝𝑝 {Γ} {A} {B} t s =
    CF-tm (Cc.𝑎𝑝𝑝 t s)
      ≡⟨ ap CF-tm (Cc.𝑎𝑝𝑝𝐴𝑝𝑝 t s) ⟩
    CF-tm (Cc.𝐴𝑝𝑝 t C.⟦ C.𝒾𝒹 Γ ⊕ s ⟧)
      ≡⟨ CF-sub (Cc.𝐴𝑝𝑝 t) (C.𝒾𝒹 Γ ⊕ s) ⟩
    CF-tm (Cc.𝐴𝑝𝑝 t) D.⟦ CF-tms (C.𝒾𝒹 Γ) ⊕ CF-tm s ⟧
      ≡⟨ (λ i → pres-𝐴𝑝𝑝 t i D.⟦ CF-id i ⊕  CF-tm s ⟧) ⟩
    Dc.𝐴𝑝𝑝 (transport (λ i → D.tm (CF-ctx Γ) (pres-⇛ A B i)) (CF-tm t))
      D.⟦ D.𝒾𝒹 (map𝐶𝑡𝑥 CF-ty Γ) ⊕ CF-tm s ⟧
      ≡⟨ Dc.𝑎𝑝𝑝𝐴𝑝𝑝 (transport (λ i → D.tm (CF-ctx Γ) (pres-⇛ A B i)) (CF-tm t)) (CF-tm s) ⁻¹ ⟩
    Dc.𝑎𝑝𝑝 (transport (λ i → D.tm (CF-ctx Γ) (pres-⇛ A B i)) (CF-tm t)) (CF-tm s)
      ∎
\end{code}

\subsection{What is Simply Typed Lambda Calculus?}

We now provide a categorical explanation of what is meant by an
\emph{implementation of STLC}. Such an implementation is going to be given by
some CCC category. Some of the types of its contextual structure will be marked
off as being \emph{Base Types}. These base types will be parametrised by some
type $X$, and the notion of an \emph{implementation of STLC} will be relative to
the type $X$. For example, when $X$ is $\top$ we get a notion of theories of
STLC with one base type.

When we fix $X$, some CCC category $\mathcal{C}$, and a function $\mathsf{base}
_\mathit{1} : X → \mathsf{ty}~\mathcal{C}$, we can ask the question of whether
$\mathcal{C}$ is the initial CCC category with respect to its notion of base
types. What this means is that for any other CCC category $\mathcal{D}$ equipped
with a marking $\mathsf{base}_\mathit{2} : X → \mathsf{ty}~\mathcal{D}$, there
is a unique CCC preserving CF $\mathcal{C} \to \mathcal{D}$ that additionally
preserves the base types.
\begin{code}
module _ {X : Type ℓ₃} (𝒞 : Contextual ℓ ℓ) ⦃ 𝒞CCC : CCC 𝒞 ⦄
         (base₁ : X → Contextual.ty 𝒞) where
  open Contextual
  open ContextualFunctor

  record InitialInstance (𝒟 : Contextual ℓ₁ ℓ₂) ⦃ 𝒟CCC : CCC 𝒟 ⦄
                         (base₂ : X → ty 𝒟) : Type (ℓ ⊔ ℓ₁ ⊔ ℓ₂ ⊔ ℓ₃) where
    BasePreserving : ContextualFunctor 𝒞 𝒟 → Type (ℓ₁ ⊔ ℓ₃)
    BasePreserving F = (A : X) → CF-ty F (base₁ A) ≡ base₂ A

    field
      elim : ContextualFunctor 𝒞 𝒟
      ccc-pres : CCCPreserving elim
      base-pres : BasePreserving elim
      UP : (F : ContextualFunctor 𝒞 𝒟) → CCCPreserving F → BasePreserving F →
        F ≡ elim

  InitialCCC = ∀ {ℓ₁} {ℓ₂} (𝒟 : Contextual ℓ₁ ℓ₂) ⦃ 𝒟CCC : CCC 𝒟 ⦄
    (base₂ : X → ty 𝒟) → InitialInstance 𝒟 base₂
\end{code}
An \emph{implementation of STLC} is then \emph{any} initial CCC category (with
respect to a fixed notion $X$ of base types). Note that, for technical reasons,
we require an initial CCC to share a common universe level for its types and
terms (this will ensure that the yoneda embedding is defined in certain presheaf
categories). The main result of this paper is a formalised proof that every
implementation of STLC admits a normalisation theorem.

\begin{code}[hide]
-- Proof that the composition of CCC preserving CFs is CCC preserving
-- Welcome to the ninth circle of transport hell

module _ {𝒞 : Contextual ℓ₁ ℓ₂} {𝒟 : Contextual ℓ₃ ℓ₄} {ℰ : Contextual ℓ₅ ℓ₆}
         ⦃ 𝒞CCC : CCC 𝒞 ⦄ ⦃ 𝒟CCC : CCC 𝒟 ⦄ ⦃ ℰCCC : CCC ℰ ⦄
         {G : ContextualFunctor 𝒟 ℰ} {F : ContextualFunctor 𝒞 𝒟} where
  open ContextualFunctor
  open CCCPreserving
  open CCC

  private
    module C = Contextual 𝒞
    module D = Contextual 𝒟
    module E = Contextual ℰ
    module Cc = CCC 𝒞CCC
    module Dc = CCC 𝒟CCC
    module Ec = CCC ℰCCC

  ∘CF-CCCPres : CCCPreserving G → CCCPreserving F → CCCPreserving (G ∘CF F)
  pres-⇛ (∘CF-CCCPres p₁ p₂) A B =
    ap (CF-ty G) (pres-⇛ p₂ A B) ∙ (pres-⇛ p₁ (CF-ty F A) (CF-ty F B))
  pres-𝐴𝑝𝑝 (∘CF-CCCPres p₁ p₂) {Γ} {A} {B} t =
    transport (λ i → E.tm (map𝐶𝑡𝑥comp (CF-ty G) (CF-ty F) (Γ ⊹ A) i) (CF-ty G (CF-ty F B)))
      (CF-tm G (CF-tm F (Cc.𝐴𝑝𝑝 t)))
      ≡⟨ ap (transport (λ i → E.tm (map𝐶𝑡𝑥comp (CF-ty G) (CF-ty F) (Γ ⊹ A) i)
        (CF-ty G (CF-ty F B)))) lem ⟩
    transport (λ i → E.tm (map𝐶𝑡𝑥comp (CF-ty G) (CF-ty F) (Γ ⊹ A) i) (CF-ty G (CF-ty F B)))
      (Ec.𝐴𝑝𝑝 (transport (λ i → E.tm (CF-ctx G (CF-ctx F Γ)) ((ap (CF-ty G) (pres-⇛ p₂ A B)
        ∙ pres-⇛ p₁ (CF-ty F A) (CF-ty F B)) i)) (CF-tm G (CF-tm F t))))
      ≡⟨ transp𝐴𝑝𝑝 ℰCCC (map𝐶𝑡𝑥comp (CF-ty G) (CF-ty F) Γ) (transport (λ i → E.tm
        (CF-ctx G (CF-ctx F Γ)) ((ap (CF-ty G) (pres-⇛ p₂ A B)
        ∙ pres-⇛ p₁ (CF-ty F A) (CF-ty F B)) i)) (CF-tm G (CF-tm F t))) ⟩
    Ec.𝐴𝑝𝑝 (transport
      (λ i → E.tm (map𝐶𝑡𝑥comp (CF-ty G) (CF-ty F) Γ i)
        (CF-ty G (CF-ty F A) Ec.⇛ CF-ty G (CF-ty F B)))
      (transport
        (λ i → E.tm (CF-ctx G (CF-ctx F Γ)) ((ap (CF-ty G) (pres-⇛ p₂ A B)
          ∙ pres-⇛ p₁ (CF-ty F A) (CF-ty F B)) i))
        (CF-tm G (CF-tm F t))))
      ≡⟨ ap Ec.𝐴𝑝𝑝 (transport-tm {tm = E.tm} refl (ap (CF-ty G) (pres-⇛ p₂ A B)
        ∙ pres-⇛ p₁ (CF-ty F A) (CF-ty F B)) (map𝐶𝑡𝑥comp (CF-ty G) (CF-ty F) Γ) refl
        (CF-tm G (CF-tm F t))) ⟩
    Ec.𝐴𝑝𝑝 (transport (λ i → E.tm
      ((refl ∙ map𝐶𝑡𝑥comp (CF-ty G) (CF-ty F) Γ) i)
      (((ap (CF-ty G) (pres-⇛ p₂ A B) ∙ pres-⇛ p₁ (CF-ty F A) (CF-ty F B)) ∙ refl) i))
      (CF-tm G (CF-tm F t)))
      ≡⟨ (λ j → Ec.𝐴𝑝𝑝 (transport (λ i → E.tm
        (rUnit (lUnit (map𝐶𝑡𝑥comp (CF-ty G) (CF-ty F) Γ) (~ j)) j i)
        (lUnit (rUnit (ap (CF-ty G) (pres-⇛ p₂ A B)
          ∙ pres-⇛ p₁ (CF-ty F A) (CF-ty F B)) (~ j)) j i))
        (CF-tm G (CF-tm F t)))) ⟩
    Ec.𝐴𝑝𝑝 (transport (λ i → E.tm
      ((map𝐶𝑡𝑥comp (CF-ty G) (CF-ty F) Γ ∙ refl) i)
      ((refl ∙ (ap (CF-ty G) (pres-⇛ p₂ A B) ∙ pres-⇛ p₁ (CF-ty F A) (CF-ty F B))) i))
      (CF-tm G (CF-tm F t)))
      ≡⟨ ap Ec.𝐴𝑝𝑝 (transport-tm {tm = E.tm} (map𝐶𝑡𝑥comp (CF-ty G) (CF-ty F) Γ) refl
        refl (ap (CF-ty G) (pres-⇛ p₂ A B) ∙ pres-⇛ p₁ (CF-ty F A) (CF-ty F B))
        (CF-tm G (CF-tm F t)) ⁻¹) ⟩
    Ec.𝐴𝑝𝑝 (transport (λ i → E.tm (map𝐶𝑡𝑥 (CF-ty G ∘ CF-ty F) Γ)
      ((ap (CF-ty G) (pres-⇛ p₂ A B) ∙ pres-⇛ p₁ (CF-ty F A) (CF-ty F B)) i))
      (transport (λ i → E.tm (map𝐶𝑡𝑥comp (CF-ty G) (CF-ty F) Γ i) (CF-ty G (CF-ty F (A Cc.⇛ B))))
        (CF-tm G (CF-tm F t))))
      ∎ where
    lem : CF-tm G (CF-tm F (Cc.𝐴𝑝𝑝 t))
      ≡ Ec.𝐴𝑝𝑝 (transport (λ i → E.tm (CF-ctx G (CF-ctx F Γ)) ((ap (CF-ty G) (pres-⇛ p₂ A B)
        ∙ pres-⇛ p₁ (CF-ty F A) (CF-ty F B)) i)) (CF-tm G (CF-tm F t)))
    lem =
      CF-tm G (CF-tm F (Cc.𝐴𝑝𝑝 t))
        ≡⟨ ap (CF-tm G) (pres-𝐴𝑝𝑝 p₂ t) ⟩
      CF-tm G (Dc.𝐴𝑝𝑝 (transport (λ i → D.tm (CF-ctx F Γ) (pres-⇛ p₂ A B i)) (CF-tm F t)))
        ≡⟨ pres-𝐴𝑝𝑝 p₁ (transport (λ i → D.tm (CF-ctx F Γ) (pres-⇛ p₂ A B i)) (CF-tm F t)) ⟩
      Ec.𝐴𝑝𝑝 (transport (λ i → E.tm (CF-ctx G (CF-ctx F Γ)) (pres-⇛ p₁ (CF-ty F A) (CF-ty F B) i))
        (CF-tm G (transport (λ i → D.tm (CF-ctx F Γ) (pres-⇛ p₂ A B i)) (CF-tm F t))))
        ≡⟨ (λ i → Ec.𝐴𝑝𝑝 (transport (λ i → E.tm (CF-ctx G (CF-ctx F Γ)) (pres-⇛ p₁ (CF-ty F A)
          (CF-ty F B) i)) (transpCF-tm G (pres-⇛ p₂ A B) (CF-tm F t) (~ i)))) ⟩
      Ec.𝐴𝑝𝑝 (transport (λ i → E.tm (CF-ctx G (CF-ctx F Γ)) (pres-⇛ p₁ (CF-ty F A) (CF-ty F B) i))
        (transport (λ i → E.tm (CF-ctx G (map𝐶𝑡𝑥 (CF-ty F) Γ)) (CF-ty G (pres-⇛ p₂ A B i)))
          (CF-tm G (CF-tm F t))))
        ≡⟨ ap Ec.𝐴𝑝𝑝 (transport-tm {tm = E.tm} refl (ap (CF-ty G) (pres-⇛ p₂ A B))
          refl (pres-⇛ p₁ (CF-ty F A) (CF-ty F B)) (CF-tm G (CF-tm F t))) ⟩
      Ec.𝐴𝑝𝑝 (transport (λ i → E.tm ((refl {x = CF-ctx G (CF-ctx F Γ)} ∙ refl) i)
        ((ap (CF-ty G) (pres-⇛ p₂ A B) ∙ pres-⇛ p₁ (CF-ty F A) (CF-ty F B)) i))
        (CF-tm G (CF-tm F t)))
        ≡⟨ (λ j → Ec.𝐴𝑝𝑝 (transport (λ i → E.tm (rUnit (refl {x = CF-ctx G (CF-ctx F Γ)}) (~ j) i)
          ((ap (CF-ty G) (pres-⇛ p₂ A B) ∙ pres-⇛ p₁ (CF-ty F A) (CF-ty F B)) i))
          (CF-tm G (CF-tm F t)))) ⟩
      Ec.𝐴𝑝𝑝 (transport (λ i → E.tm (CF-ctx G (CF-ctx F Γ)) ((ap (CF-ty G) (pres-⇛ p₂ A B)
        ∙ pres-⇛ p₁ (CF-ty F A) (CF-ty F B)) i)) (CF-tm G (CF-tm F t)))
        ∎
\end{code}
